
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Начисления.Количество() <> 0 Тогда
	
		РезультатЗапроса = ПолучитьДополнительныеДанныеПоСотрудникам(ЭтотОбъект);
		НоваяТЗдляВставкиВТЧ = РезультатЗапроса.Скопировать(,"Сотрудник, ВидРасчета, ДатаНачала, ДатаОкончания");
		
		Для каждого Строка из РезультатЗапроса Цикл
		
				Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
				
					СформироватьДвиженияПоОкладу(Строка);
					
					Если Строка.Категория = Перечисления.ВКМ_КатегорииСотрудников.Специалист Тогда
					
						СформироватьДвиженияПоПремии(Строка);
					
					КонецЕсли;
					
				ИначеЕсли Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
					
					СформироватьДвиженияПоОтпуску(Строка);
					
				Иначе
					Продолжить;
					
				КонецЕсли;
				
		КонецЦикла;
	
		Движения.ВКМ_ОсновныеНачисления.Записать();
		Движения.ВКМ_Удержания.Записать();
		
		Для Каждого Движение Из Движения.ВКМ_ОсновныеНачисления Цикл
			
			Если Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
			
				РассчитатьОклад(Движение);
			
			ИначеЕсли Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом Тогда
				
				РассчитатьПремию(Движение);
								
			ИначеЕсли Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда				
				
				РассчитатьОтпуск(Движение);
			
			КонецЕсли;
					
		КонецЦикла;
		
		Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
		Движения.ВКМ_Удержания.Записать();
					
	Иначе
			
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Табличная часть документа не может быть пустой";
		Сообщение.Сообщить();			
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьДополнительныеДанныеПоСотрудникам(ЭтотОбъект)

		// обогащение данных из табличной части документа данными об окладе, категории сотрудника и проценте премии
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Категория,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад,
		|	ВКМ_НачисленияЗарплатыНачисления.Сотрудник,
		|	ВКМ_НачисленияЗарплатыНачисления.ВидРасчета,
		|	ВКМ_НачисленияЗарплатыНачисления.ДатаНачала,
		|	ВКМ_НачисленияЗарплатыНачисления.ДатаОкончания,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот
		|ИЗ
		|	Документ.ВКМ_НачисленияЗарплаты.Начисления КАК ВКМ_НачисленияЗарплатыНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период,) КАК
		|			ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_НачисленияЗарплатыНачисления.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|ГДЕ
		|	ВКМ_НачисленияЗарплатыНачисления.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", Дата);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
		Запрос.УстановитьПараметр("Ссылка", Ссылка);	
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();	
		
		// преобразование таблицы (обеспечение наличия премии процентом для всех сотрудников категории "Специалист"
		
		СписокНачислений = Новый ТаблицаЗначений;
		СписокНачислений.Колонки.Добавить("Сотрудник");
		СписокНачислений.Колонки.Добавить("Категория");		
		СписокНачислений.Колонки.Добавить("ВидРасчета");		
		СписокНачислений.Колонки.Добавить("ДатаНачала");			
		СписокНачислений.Колонки.Добавить("ДатаОкончания");			
		СписокНачислений.Колонки.Добавить("Оклад");	
		СписокНачислений.Колонки.Добавить("ПроцентОтРабот");			

		Для Каждого Строка из РезультатЗапроса Цикл
			
			Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
				
				НоваяЗапись = СписокНачислений.Добавить();
				НоваяЗапись.Сотрудник = Строка.Сотрудник;
				НоваяЗапись.Категория = Строка.Категория;
				НоваяЗапись.ВидРасчета = Строка.ВидРасчета;
				НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
				НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
				НоваяЗапись.Оклад = Строка.Оклад;
				НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
					
				Если Строка.Категория = Перечисления.ВКМ_КатегорииСотрудников.Специалист Тогда
					
					НоваяЗапись = СписокНачислений.Добавить();
					НоваяЗапись.Сотрудник = Строка.Сотрудник;
					НоваяЗапись.Категория = Строка.Категория;
					НоваяЗапись.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом;
					НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
					НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
					НоваяЗапись.Оклад = Строка.Оклад;
					НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
			
				КонецЕсли;
			
			ИначеЕсли Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
				
				НоваяЗапись = СписокНачислений.Добавить();
				НоваяЗапись.Сотрудник = Строка.Сотрудник;
				НоваяЗапись.Категория = Строка.Категория;
				НоваяЗапись.ВидРасчета = Строка.ВидРасчета;
				НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
				НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
				НоваяЗапись.Оклад = Строка.Оклад;
				НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
				
			Иначе Продолжить;	
				
			КонецЕсли;
			
		КонецЦикла;
					
		Возврат СписокНачислений;
	
КонецФункции

Процедура СформироватьДвиженияПоОкладу(Строка)
	
	Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
	Движение.Сторно = Ложь;
	Движение.ВидРасчета = Строка.ВидРасчета;
	Движение.БазовыйПериодНачало = Строка.ДатаНачала;
	Движение.БазовыйПериодКонец = Строка.ДатаОкончания;	
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	Движение.ПериодДействияКонец = Строка.ДатаОкончания;
	Движение.ПериодРегистрации = Дата;		
	Движение.Сотрудник = Строка.Сотрудник;
	Движение.Показатель = Строка.Оклад;

КонецПроцедуры

Процедура СформироватьДвиженияПоПремии(Строка)

	Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
	Движение.Сторно = Ложь;
	Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом;
	Движение.БазовыйПериодНачало = Строка.ДатаНачала;
	Движение.БазовыйПериодКонец = Строка.ДатаОкончания;		
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	Движение.ПериодДействияКонец = Строка.ДатаОкончания;
	Движение.ПериодРегистрации = Дата;		
	Движение.Сотрудник = Строка.Сотрудник;	
	Движение.Показатель = Строка.ПроцентОтРабот;

КонецПроцедуры

Процедура СформироватьДвиженияПоОтпуску(Строка)

	Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
	Движение.Сторно = Ложь;
	Движение.ВидРасчета = Строка.ВидРасчета;
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	Движение.ПериодДействияКонец = Строка.ДатаОкончания;
	Движение.БазовыйПериодНачало = (Строка.ДатаНачала - 86400*365);
	Движение.БазовыйПериодКонец = (Строка.ДатаНачала - 86400);
	Движение.ПериодРегистрации = Дата;		
	Движение.Сотрудник = Строка.Сотрудник;
	Движение.Показатель = 0;
	
КонецПроцедуры

Процедура РассчитатьОклад(Движение)

	План = Движение.ПолучитьДанныеГрафика(ВидПериодаРегистраРасчета.ПериодДействия);
	Факт = Движение.ПолучитьДанныеГрафика(ВидПериодаРегистраРасчета.ФактическийПериодДействия);
	
	Результат = Движение.Показатель/План[0].РабочихДней*Факт[0].РабочихДней;
	
	Движение.Сумма = Результат;
	
	СформироватьДвижениеУдержания(Движение);
	
КонецПроцедуры

Процедура РассчитатьПремию(Движение)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеПриход
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода,,
	|		Сотрудник = &Сотрудник) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты";
	
	Запрос.УстановитьПараметр("Сотрудник", Движение.Сотрудник);
	Запрос.УстановитьПараметр("НачалоПериода", Движение.ПериодДействияНачало);				
	Запрос.УстановитьПараметр("КонецПериода", Движение.ПериодДействияКонец);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если РезультатЗапроса.Количество() <> 0 Тогда
		
		Движение.Сумма = РезультатЗапроса.ВыгрузитьКолонку("СуммаКОплатеПриход")[0];
	
	Иначе 
	
		Движение.Сумма = 0;
	
	КонецЕсли;
	
		СформироватьДвижениеУдержания(Движение);
		
КонецПроцедуры

Процедура РассчитатьОтпуск(Движение)

	ДанныеГрафика = ПолучитьДанныеГрафика(Движение);
	
	Если ДанныеГрафика.Количество() <> 0 Тогда

		БазаИтог = ПолучитьБазу(Движение);
			
		КоличествоОтработанныхДней = ДанныеГрафика[0].РабочихДнейПериодДействия;
		КоличествоДнейОтпуска = ДанныеГрафика[0].ФактическийПериодДействия;
		СтоимостьДняОтпуска = БазаИтог/КоличествоОтработанныхДней;
		Движение.Сумма = СтоимостьДняОтпуска * КоличествоДнейОтпуска;
		Движение.Показатель = СтоимостьДняОтпуска;
	
	Иначе
		
		Движение.Сумма = 0;
		
	КонецЕсли;
	
		СформироватьДвижениеУдержания(Движение);
	
КонецПроцедуры

Функция ПолучитьДанныеГрафика(Движение)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейФактическийПериодДействия КАК ФактическийПериодДействия,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейБазовыйПериод,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейПериодДействия,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейПериодРегистрации
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(БазовыйПериодНачало = &БазовыйПериодНачало
	|	И БазовыйПериодКонец = &БазовыйПериодКонец) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|ГДЕ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник = &Сотрудник
	|	И ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета = &Отпуск";
	
	Запрос.УстановитьПараметр("БазовыйПериодНачало", Движение.БазовыйПериодНачало);
	Запрос.УстановитьПараметр("БазовыйПериодКонец", Движение.БазовыйПериодКонец);
	Запрос.УстановитьПараметр("Сотрудник", Движение.Сотрудник);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	
	Возврат Запрос.Выполнить().Выгрузить();	
	
КонецФункции

Функция ПолучитьБазу(Движение)
	
	РесурсыОсновныеНачисления = Новый Массив(1);
	РесурсыОсновныеНачисления[0] = "ВКМ_ОсновныеНачисления.Сумма";

	РесурсыДополнительныеНачисления = Новый Массив(1);
	РесурсыДополнительныеНачисления[0] = "ВКМ_ДополнительныеНачисления.Сумма";
	
//	РесурсыУдержания = Новый Массив(1);
//	РесурсыУдержания[0] = "ВКМ_Удержания.Сумма";
	
	ИзмеренияОсновныеНачисления = Новый Структура("Сотрудник");
	ИзмеренияОсновныеНачисления.Сотрудник = "ВКМ_ОсновныеНачисления.Сотрудник";

	ИзмеренияДополнительныеНачисления = Новый Структура("Сотрудник");
	ИзмеренияДополнительныеНачисления.Сотрудник = "ВКМ_ДополнительныеНачисления.Сотрудник";
	
//	ИзмеренияУдержания = Новый Структура("Сотрудник");
//	ИзмеренияУдержания.Сотрудник = "ВКМ_Удержания.Сотрудник";	
		
	БазаОсновныеНачисления = Движение.ПолучитьБазу(РесурсыОсновныеНачисления, ИзмеренияОсновныеНачисления);
	БазаДополнительныеНачисления = Движение.ПолучитьБазу(РесурсыДополнительныеНачисления, ИзмеренияДополнительныеНачисления);	
//	БазаУдержания = Движение.ПолучитьБазу(РесурсыУдержания, ИзмеренияУдержания);
	
	БазаИтог = БазаОсновныеНачисления[0].Сумма + БазаДополнительныеНачисления[0].Сумма;
	
	Возврат БазаИтог;
	
КонецФункции

Процедура СформироватьДвижениеУдержания(Движение)

	Если Движение.Сумма <> 0 Тогда
		
		ДвижениеУдержание = Движения.ВКМ_Удержания.Добавить();
		ДвижениеУдержание.Сторно = Ложь;
		ДвижениеУдержание.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ДвижениеУдержание.БазовыйПериодНачало = Движение.БазовыйПериодНачало;
		ДвижениеУдержание.БазовыйПериодКонец = Движение.БазовыйПериодКонец;	
		ДвижениеУдержание.ПериодДействияНачало = Движение.ПериодДействияНачало;
		ДвижениеУдержание.ПериодДействияКонец = Движение.ПериодДействияКонец;
		ДвижениеУдержание.ПериодРегистрации = Движение.ПериодРегистрации;
		ДвижениеУдержание.Сотрудник = Движение.Сотрудник;
		ДвижениеУдержание.Сумма = Движение.Сумма * 0.13;	
		ДвижениеУдержание.Комментарий = Движение.ВидРасчета;

	КонецЕсли;

КонецПроцедуры
