
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

//++ ВКМ_Дроздков_01.07.24
#Область ВКМ_Доработки

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт
		
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
	
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктВыполненныхРабот";
	КомандаПечати.Представление = НСтр("ru = 'Акт выполненных работ'");
	КомандаПечати.Порядок = 5;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктВыполненныхРабот");
	
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ОфисныеДокументы = ПечатьАктаВР(МассивОбъектов);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт выполненных работ'");

	КонецЕсли;
	
КонецПроцедуры

Функция ПечатьАктаВР(МассивОбъектов)
	
    ОфисныеДокументы = Новый Соответствие;
 
    МакетДокумента = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_DOC_АктВыполненныхРабот");
    Макет = УправлениеПечатью.ИнициализироватьМакетОфисногоДокумента(МакетДокумента, Неопределено);  
        
    ОписаниеОбластей = Новый Структура; 
	УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Шапка", "Общая");
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Основное", "Общая"); 
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "ЗаголовокТаблицы", "СтрокаТаблицы"); 
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "СтрокаТаблицы", "СтрокаТаблицы"); 
    УправлениеПечатью.ДобавитьОписаниеОбласти(ОписаниеОбластей, "Подвал", "Общая"); 
	
    ДанныеДляПечати = ПолучитьДанныеДокументов(МассивОбъектов);
		
    Пока ДанныеДляПечати.Следующий() Цикл 
        
		ПечатнаяФорма = УправлениеПечатью.ИнициализироватьПечатнуюФорму(Неопределено, Неопределено, Макет);
		
		ШаблонЗаголовка = "Акт выполненных работ № %1 от %2";
		ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДляПечати.Номер),Формат(ДанныеДляПечати.Дата,"ДЛФ=DD" ));
		Контрагент = ДанныеДляПечати.Контрагент;
		Организация = ДанныеДляПечати.Организация;	
		Договор = ДанныеДляПечати.Договор;
		ТаблицаЗначенийУслуги = ДанныеДляПечати.Услуги.Выгрузить();
		СуммаУслуг = 0;
		
		Для каждого Услуга из ТаблицаЗначенийУслуги Цикл
		СуммаУслуг = СуммаУслуг + Услуга.Сумма;		
		КонецЦикла;	
		
		ФормСтрока = "Л = ru_RU; ДП = Истина";
		ПарПредмета="рубль,рубля,рублей,м,копейка,копейки,копеек,м,2";
		СуммаУслугПрописью = ЧислоПрописью(СуммаУслуг, ФормСтрока, ПарПредмета);
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ТекстЗаголовка", ТекстЗаголовка);
		СтруктураПараметров.Вставить("Контрагент", Контрагент);
		СтруктураПараметров.Вставить("Организация", Организация);
		СтруктураПараметров.Вставить("Договор", Договор);
		СтруктураПараметров.Вставить("Услуги", ТаблицаЗначенийУслуги);
		СтруктураПараметров.Вставить("СуммаУслуг", СуммаУслуг);
		СтруктураПараметров.Вставить("СуммаУслугПрописью", СуммаУслугПрописью);

        Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.Шапка); 
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, СтруктураПараметров);
		//УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеДокумента);
		
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.Основное); 
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, СтруктураПараметров);   
		
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.ЗаголовокТаблицы); 
		УправлениеПечатью.ПрисоединитьОбласть(ПечатнаяФорма, Область, Ложь);
		
		ДанныеПечатиНоменклатура = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаЗначенийУслуги);
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.СтрокаТаблицы); 
		УправлениеПечатью.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, Область, ДанныеПечатиНоменклатура);
		
		Область = УправлениеПечатью.ОбластьМакета(Макет, ОписаниеОбластей.Подвал); 
		УправлениеПечатью.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, СтруктураПараметров);  
       		
        АдресХранилищаПечатнойФормы = УправлениеПечатью.СформироватьДокумент(ПечатнаяФорма);
        ОфисныеДокументы.Вставить(АдресХранилищаПечатнойФормы, Строка(ДанныеДляПечати.Ссылка));
        УправлениеПечатью.ОчиститьСсылки(ПечатнаяФорма);
        
    КонецЦикла;        
    
    УправлениеПечатью.ОчиститьСсылки(Макет);
    
    Возврат ОфисныеДокументы;
	
	
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Номер,
	|	РеализацияТоваровУслуг.Дата,
	|	РеализацияТоваровУслуг.Проведен,
	|	РеализацияТоваровУслуг.Организация,
	|	РеализацияТоваровУслуг.Контрагент,
	|	РеализацияТоваровУслуг.Договор,
	|	РеализацияТоваровУслуг.СуммаДокумента,
	|	РеализацияТоваровУслуг.Основание,
	|	РеализацияТоваровУслуг.Ответственный,
	|	РеализацияТоваровУслуг.Комментарий,
	|	РеализацияТоваровУслуг.Товары.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма),
	|	РеализацияТоваровУслуг.Услуги.(
	|		Ссылка,
	|		НомерСтроки,
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма),
	|	РеализацияТоваровУслуг.Представление,
	|	РеализацияТоваровУслуг.МоментВремени
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Возврат Запрос.Выполнить().Выбрать();

КонецФункции

#КонецОбласти
//--

#КонецЕсли