
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Очистка предыдущих движений регистра накопления "ВзаиморасчетыССотрудниками"
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Очистить();
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать(); 
	
	//++ Исправление замечаний к дипломной работе 30.07.24
	//Получение списка сотрудников без дублей
	
	Запрос = Новый Запрос;       
	
	Запрос.Текст =  "ВЫБРАТЬ
	                |	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Сотрудник КАК Сотрудник,
	                |	СУММА(ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Сумма) КАК Сумма
	                |ИЗ
	                |	Документ.ВКМ_НачислениеФиксированнойПремии.СписокСотрудников КАК ВКМ_НачислениеФиксированнойПремииСписокСотрудников
	                |ГДЕ
	                |	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Ссылка = &Ссылка
	                |
	                |СГРУППИРОВАТЬ ПО
	                |	ВКМ_НачислениеФиксированнойПремииСписокСотрудников.Сотрудник";   
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ТЗ = Запрос.Выполнить().Выгрузить();
	
	//--  
	
	//Формирование движений
	
	СформироватьДвижениеПоРегиструДополнительныеНачисления(ТЗ);
	СформироватьДвижениеПоРегиструУдержания(ТЗ);
		
	Движения.ВКМ_Удержания.Записать();
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
КонецПроцедуры

Процедура 	СформироватьДвижениеПоРегиструДополнительныеНачисления(ТЗ)
	
	Для Каждого Строка Из ТЗ Цикл
	
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ФиксированнаяПремия;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = НачалоДня(КонецМесяца(Дата));
		Движение.ПериодДействияНачало = НачалоМесяца(Дата);
		Движение.ПериодДействияКонец = НачалоДня(КонецМесяца(Дата));
		Движение.ПериодРегистрации = Дата;		
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Сумма = Строка.Сумма;
		
		СформироватьПриходПоРегиструНакопленияВзаиморасчетыССотрудниками(Движение);  
		
	КонецЦикла;
	
		Движения.ВКМ_ДополнительныеНачисления.Записать();
		
КонецПроцедуры

Процедура 	СформироватьПриходПоРегиструНакопленияВзаиморасчетыССотрудниками(Движение)
	
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
		ДвижениеНакопление = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		ДвижениеНакопление.Период = Движение.ПериодДействияКонец;
		ДвижениеНакопление.Сотрудник = Движение.Сотрудник;
		ДвижениеНакопление.ВидРасчета = Движение.ВидРасчета;
		ДвижениеНакопление.Сумма = Движение.Сумма; 
		
КонецПроцедуры

Процедура 	СформироватьДвижениеПоРегиструУдержания(ТЗ)
	
	Для каждого Строка Из ТЗ Цикл
			
		ДвижениеУдержание = Движения.ВКМ_Удержания.Добавить();
		ДвижениеУдержание.Сторно = Ложь;
		ДвижениеУдержание.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ДвижениеУдержание.БазовыйПериодНачало = НачалоМесяца(Дата);
		ДвижениеУдержание.БазовыйПериодКонец = НачалоДня(КонецМесяца(Дата));
		ДвижениеУдержание.ПериодДействияНачало = НачалоМесяца(Дата);
		ДвижениеУдержание.ПериодДействияКонец = НачалоДня(КонецМесяца(Дата));
		ДвижениеУдержание.ПериодРегистрации = Дата;
		ДвижениеУдержание.Сотрудник = Строка.Сотрудник;
		ДвижениеУдержание.Комментарий = ПолучитьКомментарийКДвижениямПоНДФЛДляДополнительныхНачислений();   
		
	КонецЦикла;

	Движения.ВКМ_Удержания.Записать();
	РасчитатьНДФЛ();
                  	
КонецПроцедуры  

Процедура РасчитатьНДФЛ()

	Запрос = Новый Запрос;
	
	Запрос.Текст = 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	               	|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
	               	|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
	               	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез КАК РегистраторРазрез,
	               	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.СуммаБаза КАК ДопНачисления,
	               	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	               	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец
	               	|ИЗ
	               	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	               	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ДополнительныеНачисления(
	               	|				&Измерения,
	               	|				&Измерения,
	               	|				&Разрезы,
	               	|				Регистратор = &Ссылка
	               	|					И ВидРасчета = &НДФЛ) КАК ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления
	               	|		ПО ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.НомерСтроки
	               	|			И ВКМ_Удержания.Сотрудник = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.Сотрудник
	               	|ГДЕ
	               	|	ВКМ_Удержания.Регистратор = &Ссылка
	               	|	И ВКМ_Удержания.ВидРасчета = &НДФЛ
	               	|	И ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез.Ссылка = &Ссылка
	               	|
	               	|СГРУППИРОВАТЬ ПО
	               	|	ВКМ_Удержания.Сотрудник,
	               	|	ВКМ_Удержания.НомерСтроки,
	               	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.СуммаБаза,
	               	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез,
	               	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.БазовыйПериодНачало,
	               	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.БазовыйПериодКонец"; 
		
	Измерения = Новый Массив();
	Измерения.Добавить("Сотрудник");

	Разрезы = Новый Массив();
	Разрезы.Добавить("Регистратор");  
	
	Запрос.УстановитьПараметр("Разрезы", Разрезы);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);  
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Если Выборка.ДопНачисления <> 0 Тогда
			
			ДвижениеРасчетНДФЛ = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1]; 
			
			СтавкаНДФЛ = 0.13;
			СуммаНДФЛНачислено = Выборка.ДопНачисления*СтавкаНДФЛ;
								
			ДвижениеРасчетНДФЛ.Сумма = СуммаНДФЛНачислено; 
			
			СформироватьРасходПоРегиструНакопленияВзаиморасчетыССотрудниками(ДвижениеРасчетНДФЛ);
			
		Иначе 
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Сумма НДФЛ не рассчитана. Обратитесь к администратору системы";  
			Сообщение.Сообщить ();
			
		КонецЕсли;
	
	КонецЦикла;
		
КонецПроцедуры

Функция ПолучитьКомментарийКДвижениямПоНДФЛДляДополнительныхНачислений()
	
		Возврат Строка(ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ФиксированнаяПремия);
	
КонецФункции


Процедура 	СформироватьРасходПоРегиструНакопленияВзаиморасчетыССотрудниками(Движение)
	
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
		ДвижениеНакопление = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьРасход();
		ДвижениеНакопление.Период = Движение.ПериодДействияКонец;
		ДвижениеНакопление.Сотрудник = Движение.Сотрудник;
		ДвижениеНакопление.ВидРасчета = Движение.ВидРасчета;
		ДвижениеНакопление.Сумма = Движение.Сумма; 
		ДвижениеНакопление.Комментарий = Движение.Комментарий;
		
КонецПроцедуры
