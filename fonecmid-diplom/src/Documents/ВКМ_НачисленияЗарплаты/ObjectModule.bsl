
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Начисления.Количество() <> 0 Тогда
	
		//Получение модифицированной таблицы с полным набором строк и столбцов
		// и с процентом премии для всех сотрудников категории "Специалист"
		
		РезультатЗапроса = ПолучитьДополнительныеДанныеПоСотрудникам(ЭтотОбъект);
		
		ЕстьСотрудникиСНеустановленнымиОкладамиИлиПремиями = Ложь;
		МассивСотрудниковСНеустановленнымиОкладамиИлиПремиями = Новый Массив;
		
		Для каждого Строка из РезультатЗапроса Цикл
		
			Если НЕ ЗначениеЗаполнено(Строка.Оклад) Тогда	
				ЕстьСотрудникиСНеустановленнымиОкладамиИлиПремиями = Истина;
				МассивСотрудниковСНеустановленнымиОкладамиИлиПремиями.Добавить(Строка(Строка.Сотрудник));
			Иначе
				Если Строка.Категория = "Специалист" ИЛИ НЕ ЗначениеЗаполнено(Строка.Категория) Тогда
					Если НЕ ЗначениеЗаполнено(Строка.ПроцентОтРабот)Тогда
						ЕстьСотрудникиСНеустановленнымиОкладамиИлиПремиями = Истина;
						МассивСотрудниковСНеустановленнымиОкладамиИлиПремиями.Добавить(Строка(Строка.Сотрудник));
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ ЕстьСотрудникиСНеустановленнымиОкладамиИлиПремиями Тогда
		
			НоваяТЗдляВставкиВТЧ = РезультатЗапроса.Скопировать(,"Сотрудник, ВидРасчета, ДатаНачала, ДатаОкончания");
			
			//Очистка предыдущих движений регистра накопления "ВзаиморасчетыССотрудниками"
					
			Движения.ВКМ_ВзаиморасчетыССотрудниками.Очистить();
			Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
			Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать(); 
			
			//Формирование движений
			
			Для каждого Строка из РезультатЗапроса Цикл
			
					Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
					
						СформироватьДвиженияПоОкладу(Строка);
						
						Если Строка.Категория = Перечисления.ВКМ_КатегорииСотрудников.Специалист Тогда
						
							СформироватьДвиженияПоПремии(Строка);
						
						КонецЕсли;
						
					ИначеЕсли Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
						
						СформироватьДвиженияПоОтпуску(Строка);
						
					Иначе
						Продолжить;
						
					КонецЕсли;
					
			КонецЦикла;
		
			Движения.ВКМ_ОсновныеНачисления.Записать();
			Движения.ВКМ_Удержания.Записать();
			
			Для Каждого Движение Из Движения.ВКМ_ОсновныеНачисления Цикл
				
				Если Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
				
					РассчитатьОклад(Движение);
				
				ИначеЕсли Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом Тогда
					
					РассчитатьПремию(Движение);
									
				ИначеЕсли Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда				
					
					РассчитатьОтпуск(Движение);
				
				КонецЕсли;
						
			КонецЦикла;
			
			Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
			Движения.ВКМ_Удержания.Записать();
			Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
			
			ДокументОбъект = Ссылка.ПолучитьОбъект();
			ДокументОбъект.Начисления.Очистить();
			ДокументОбъект.Начисления.Загрузить(НоваяТЗдляВставкиВТЧ);	
			ДокументОбъект.Записать();	
		
		Иначе
			
			СписокСотрудниковСНеустановленнымиОкладамиИлиПремиями = СтрСоединить(МассивСотрудниковСНеустановленнымиОкладамиИлиПремиями, ", ");
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Для следующих сотрудников не установлены оклад или премия в указанном периоде:" + Символы.ПС + СписокСотрудниковСНеустановленнымиОкладамиИлиПремиями;
			Сообщение.Сообщить();	
			
			Отказ = Истина;
			
		КонецЕсли;		
				
	Иначе
			
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Табличная часть документа не может быть пустой";
		Сообщение.Сообщить();			
		
		Отказ = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

Функция ПолучитьДополнительныеДанныеПоСотрудникам(ЭтотОбъект)

		// обогащение данных из табличной части документа данными об окладе, категории сотрудника и проценте премии
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Категория,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад,
		|	ВКМ_НачисленияЗарплатыНачисления.Сотрудник,
		|	ВКМ_НачисленияЗарплатыНачисления.ВидРасчета,
		|	ВКМ_НачисленияЗарплатыНачисления.ДатаНачала,
		|	ВКМ_НачисленияЗарплатыНачисления.ДатаОкончания,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот
		|ИЗ
		|	Документ.ВКМ_НачисленияЗарплаты.Начисления КАК ВКМ_НачисленияЗарплатыНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период,) КАК
		|			ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|		ПО ВКМ_НачисленияЗарплатыНачисления.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
		|ГДЕ
		|	ВКМ_НачисленияЗарплатыНачисления.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Период", Дата);
		Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
		Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
		Запрос.УстановитьПараметр("Ссылка", Ссылка);	
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();	
		
		// преобразование таблицы (обеспечение наличия премии процентом для всех сотрудников категории "Специалист"
		
		СписокНачислений = Новый ТаблицаЗначений;
		СписокНачислений.Колонки.Добавить("Сотрудник");
		СписокНачислений.Колонки.Добавить("Категория");		
		СписокНачислений.Колонки.Добавить("ВидРасчета");		
		СписокНачислений.Колонки.Добавить("ДатаНачала");			
		СписокНачислений.Колонки.Добавить("ДатаОкончания");			
		СписокНачислений.Колонки.Добавить("Оклад");	
		СписокНачислений.Колонки.Добавить("ПроцентОтРабот");			

		Для Каждого Строка из РезультатЗапроса Цикл
			
			Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
								
				НоваяЗапись = СписокНачислений.Добавить();
				НоваяЗапись.Сотрудник = Строка.Сотрудник;
				НоваяЗапись.Категория = Строка.Категория;
				НоваяЗапись.ВидРасчета = Строка.ВидРасчета;
				НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
				НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
				НоваяЗапись.Оклад = Строка.Оклад;
				НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
					
				Если Строка.Категория = Перечисления.ВКМ_КатегорииСотрудников.Специалист Тогда
					
					НоваяЗапись = СписокНачислений.Добавить();
					НоваяЗапись.Сотрудник = Строка.Сотрудник;
					НоваяЗапись.Категория = Строка.Категория;
					НоваяЗапись.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом;
					НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
					НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
					НоваяЗапись.Оклад = Строка.Оклад;
					НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
			
				КонецЕсли;
			
			ИначеЕсли Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
				
				НоваяЗапись = СписокНачислений.Добавить();
				НоваяЗапись.Сотрудник = Строка.Сотрудник;
				НоваяЗапись.Категория = Строка.Категория;
				НоваяЗапись.ВидРасчета = Строка.ВидРасчета;
				НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
				НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
				НоваяЗапись.Оклад = Строка.Оклад;
				НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
				
			Иначе Продолжить;	
				
			КонецЕсли;
			
		КонецЦикла;
					
		Возврат СписокНачислений;
	
КонецФункции

Процедура СформироватьДвиженияПоОкладу(Строка)
	
	Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
	Движение.Сторно = Ложь;
	Движение.ВидРасчета = Строка.ВидРасчета;
	Движение.БазовыйПериодНачало = Строка.ДатаНачала;
	Движение.БазовыйПериодКонец = Строка.ДатаОкончания;	
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	Движение.ПериодДействияКонец = Строка.ДатаОкончания;
	Движение.ПериодРегистрации = Дата;		
	Движение.Сотрудник = Строка.Сотрудник;
	Движение.Показатель = Строка.Оклад;
	
КонецПроцедуры

Процедура СформироватьДвиженияПоПремии(Строка)

	Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
	Движение.Сторно = Ложь;
	Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом;
	Движение.БазовыйПериодНачало = Строка.ДатаНачала;
	Движение.БазовыйПериодКонец = Строка.ДатаОкончания;		
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	Движение.ПериодДействияКонец = Строка.ДатаОкончания;
	Движение.ПериодРегистрации = Дата;		
	Движение.Сотрудник = Строка.Сотрудник;	
	Движение.Показатель = Строка.ПроцентОтРабот;

КонецПроцедуры

Процедура СформироватьДвиженияПоОтпуску(Строка)

	Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
	Движение.Сторно = Ложь;
	Движение.ВидРасчета = Строка.ВидРасчета;
	Движение.ПериодДействияНачало = Строка.ДатаНачала;
	Движение.ПериодДействияКонец = Строка.ДатаОкончания;
	Движение.БазовыйПериодНачало = (Строка.ДатаНачала - 86400*365);
	Движение.БазовыйПериодКонец = (Строка.ДатаНачала - 86400);
	Движение.ПериодРегистрации = Дата;		
	Движение.Сотрудник = Строка.Сотрудник;
	Движение.Показатель = 0;
	
КонецПроцедуры

Процедура РассчитатьОклад(Движение)

	План = Движение.ПолучитьДанныеГрафика(ВидПериодаРегистраРасчета.ПериодДействия);
	Факт = Движение.ПолучитьДанныеГрафика(ВидПериодаРегистраРасчета.ФактическийПериодДействия);
	
	Результат = Движение.Показатель/План[0].РабочихДней*Факт[0].РабочихДней;
	
	Движение.Сумма = Результат;
	
	СформироватьДвижениеУдержания(Движение);
	СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(Движение);
	
КонецПроцедуры

Процедура РассчитатьПремию(Движение)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеПриход
	|ИЗ
	|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода,,
	|		Сотрудник = &Сотрудник) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты";
	
	Запрос.УстановитьПараметр("Сотрудник", Движение.Сотрудник);
	Запрос.УстановитьПараметр("НачалоПериода", Движение.ПериодДействияНачало);				
	Запрос.УстановитьПараметр("КонецПериода", Движение.ПериодДействияКонец);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если РезультатЗапроса.Количество() <> 0 Тогда
		
		Движение.Сумма = РезультатЗапроса.ВыгрузитьКолонку("СуммаКОплатеПриход")[0];
	
	Иначе 
	
		Движение.Сумма = 0;
	
	КонецЕсли;
	
		СформироватьДвижениеУдержания(Движение);
		СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(Движение);
		
КонецПроцедуры

Процедура РассчитатьОтпуск(Движение)

	РабочихДнейВБазовомПериоде = Движение.ПолучитьДанныеГрафика(ВидПериодаРегистраРасчета.БазовыйПериод)[0].РабочихДней;
	КоличествоДнейОтпуска = ((Движение.ПериодДействияКонец - Движение.ПериодДействияНачало)/86400)+1;

	Если РабочихДнейВБазовомПериоде <> 0 Тогда

		БазаИтог = ПолучитьБазу(Движение);
			
		СтоимостьДняОтпуска = БазаИтог/РабочихДнейВБазовомПериоде;
		Движение.Сумма = СтоимостьДняОтпуска * КоличествоДнейОтпуска;
		Движение.Показатель = СтоимостьДняОтпуска;
	
	Иначе
		
		Движение.Сумма = 0;
		
	КонецЕсли;
	
		СформироватьДвижениеУдержания(Движение);
		СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(Движение);
	
КонецПроцедуры

Функция ПолучитьБазу(Движение)
	
	РесурсыОсновныеНачисления = Новый Массив(1);
	РесурсыОсновныеНачисления[0] = "ВКМ_ОсновныеНачисления.Сумма";

	РесурсыДополнительныеНачисления = Новый Массив(1);
	РесурсыДополнительныеНачисления[0] = "ВКМ_ДополнительныеНачисления.Сумма";
	
	ИзмеренияОсновныеНачисления = Новый Структура("Сотрудник");
	ИзмеренияОсновныеНачисления.Сотрудник = "ВКМ_ОсновныеНачисления.Сотрудник";

	ИзмеренияДополнительныеНачисления = Новый Структура("Сотрудник");
	ИзмеренияДополнительныеНачисления.Сотрудник = "ВКМ_ДополнительныеНачисления.Сотрудник";
	
	БазаОсновныеНачисления = Движение.ПолучитьБазу(РесурсыОсновныеНачисления, ИзмеренияОсновныеНачисления);
	БазаДополнительныеНачисления = Движение.ПолучитьБазу(РесурсыДополнительныеНачисления, ИзмеренияДополнительныеНачисления);	
	
	БазаИтог = БазаОсновныеНачисления[0].Сумма + БазаДополнительныеНачисления[0].Сумма;
	
	Возврат БазаИтог;
	
КонецФункции

Процедура СформироватьДвижениеУдержания(Движение)

	Если Движение.Сумма <> 0 Тогда
		
		ДвижениеУдержание = Движения.ВКМ_Удержания.Добавить();
		ДвижениеУдержание.Сторно = Ложь;
		ДвижениеУдержание.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ДвижениеУдержание.БазовыйПериодНачало = Движение.БазовыйПериодНачало;
		ДвижениеУдержание.БазовыйПериодКонец = Движение.БазовыйПериодКонец;	
		ДвижениеУдержание.ПериодДействияНачало = Движение.ПериодДействияНачало;
		ДвижениеУдержание.ПериодДействияКонец = Движение.ПериодДействияКонец;
		ДвижениеУдержание.ПериодРегистрации = Движение.ПериодРегистрации;
		ДвижениеУдержание.Сотрудник = Движение.Сотрудник;
		ДвижениеУдержание.Сумма = Движение.Сумма * 0.13;	
		ДвижениеУдержание.Комментарий = Движение.ВидРасчета;
		
		СформироватьРасходПоРегиструВзаиморасчетыССотрудниками(ДвижениеУдержание);

	КонецЕсли;

КонецПроцедуры

Процедура СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(Движение)
	
	Если Движение.Сумма <> 0 Тогда
		
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
		ДвижениеНакопление = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		ДвижениеНакопление.Период = Движение.ПериодДействияКонец;
		ДвижениеНакопление.Сотрудник = Движение.Сотрудник;
		ДвижениеНакопление.ВидРасчета = Движение.ВидРасчета;
		ДвижениеНакопление.Сумма = Движение.Сумма; 
			
	КонецЕсли;		
	
КонецПроцедуры

Процедура СформироватьРасходПоРегиструВзаиморасчетыССотрудниками(Движение)
	
	Если Движение.Сумма <> 0 Тогда
		
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
		ДвижениеНакопление = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьРасход();
		ДвижениеНакопление.Период = Движение.ПериодДействияКонец;
		ДвижениеНакопление.Сотрудник = Движение.Сотрудник;
		ДвижениеНакопление.ВидРасчета = Движение.ВидРасчета;
		ДвижениеНакопление.Сумма = Движение.Сумма; 
		ДвижениеНакопление.Комментарий = Движение.Комментарий;
			
	КонецЕсли;		
	
КонецПроцедуры
