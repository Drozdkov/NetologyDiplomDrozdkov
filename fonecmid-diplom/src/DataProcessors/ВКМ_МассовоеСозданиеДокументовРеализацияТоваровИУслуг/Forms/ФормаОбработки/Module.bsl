&НаКлиенте
Процедура Заполнить(Команда)
	
	Если ЗначениеЗаполнено(Объект.СозданныеДокументы) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьСозданиеДокументовРеализацияТоваровИУслуг", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения, "Список созданных документов будет очищен." + Символы.ПС + "Продолжить?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
	Иначе
		ВыполнитьСозданиеДокументовРеализацияТоваровИУслуг(КодВозвратаДиалога.Да, Новый Структура);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСозданиеДокументовРеализацияТоваровИУслуг(РезультатВыбора, ДопПараметры) Экспорт

	Если РезультатВыбора = КодВозвратаДиалога.Да Тогда
		
		Если ЗначениеЗаполнено(Объект.Период) Тогда
			
			НачалоПериода = НачалоМесяца(Объект.Период);
			КонецПериода = КонецМесяца(Объект.Период);
	
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
			ПараметрыОжидания.Интервал = 1;
			ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
			ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
		
			ОповещениеОЗавешении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);
			ОповещениеОПрогрессе = Новый ОписаниеОповещения("ОповещениеОПрогрессе", ЭтотОбъект);
			ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = ОповещениеОПрогрессе;
			
			МассивСозданныхДокументов = ЗаполнитьНаСервере(НачалоПериода, КонецПериода);
			
			ДлительныеОперацииКлиент.ОжидатьЗавершение(МассивСозданныхДокументов, ОповещениеОЗавешении, ПараметрыОжидания);
			
		Иначе
		
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Необходимо заполнить период для автосоздания документов!";
			Сообщение.Сообщить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьНаСервере(НачалоПериода, КонецПериода)
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор,"Обработки.ВКМ_МассовоеСозданиеДокументовРеализацияТоваровИУслуг.ВыполнитьСозданиеДокументовРеализация", НачалоПериода, КонецПериода,);
	
	Возврат ДлительнаяОперация;
		
КонецФункции

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
	
		МассивСозданныхДокументов = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
			Если МассивСозданныхДокументов.Количество() <> 0 Тогда
			
				Объект.СозданныеДокументы.Очистить();
				
				Для Каждого СозданныйДокумент Из МассивСозданныхДокументов Цикл
					НоваяСтрока = Объект.СозданныеДокументы.Добавить();
					НоваяСтрока.Договор = СозданныйДокумент.Договор;
					НоваяСтрока.ДокументРеализацияТоваровИУслуг = СозданныйДокумент.РеализацияТоваровИУслуг;
				КонецЦикла;
				
				Сообщения = Результат.Сообщения;
				
				Если Сообщения.Количество() <> 0 Тогда
				
					Для каждого Сообщение Из Сообщения Цикл
						
						Сообщить(Сообщение);
						
					КонецЦикла;
										
				КонецЕсли;
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Документы успешно созданы. Количество созданных документов: %1", МассивСозданныхДокументов.Количество());
				Сообщение.Сообщить();
												
			Иначе
				
				Объект.СозданныеДокументы.Очистить();
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Данные для создания новых документов отсутствуют.";
				Сообщение.Сообщить();
			
			КонецЕсли;
		
	КонецЕслИ;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОПрогрессе(Прогресс, ДополнительныеПараметры) Экспорт
	
	Если Прогресс.Прогресс <> Неопределено Тогда
	
		Состояние("Прогресс: ", Прогресс.Прогресс.Процент);
		
	КонецЕслИ;
	
КонецПроцедуры