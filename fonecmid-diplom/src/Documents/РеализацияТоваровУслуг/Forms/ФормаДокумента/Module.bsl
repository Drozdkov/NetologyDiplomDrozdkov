
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//++ Дроздков_ВКМ 24.06.24
	ВКМДобавитьКомандуАвтозаполненияДокумента();
	ВКМНастроитьДоступностьКомандыЗаполненияДокумента();
	//--
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

//++ Дроздков_ВКМ 24.06.24
#Область ВКМ_Доработки

&НаСервере
Процедура ВКМДобавитьКомандуАвтозаполненияДокумента()
	
	КомандаЗаполнить = Команды.Добавить("Заполнить");
	КомандаЗаполнить.Заголовок = "Заполнить содержимое документа";
	КомандаЗаполнить.Действие = "КомандаЗаполнить";
	
	КнопкаКомандыЗаполнить = Элементы.Добавить("КнопкаЗаполнить", Тип("КнопкаФормы"), Элементы.ГруппаШапкаЛево);
	КнопкаКомандыЗаполнить.ИмяКоманды = "Заполнить";
	КнопкаКомандыЗаполнить.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаКомандыЗаполнить.ЦветФона = ЦветаСтиля.ЦветВажного;
	КнопкаКомандыЗаполнить.Доступность = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ВКМНастроитьДоступностьКомандыЗаполненияДокумента()
	
	Если ЗначениеЗаполнено(Объект.Договор) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ДоговорыКонтрагентов.ВидДоговора
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Объект.Договор);
		ВидДоговора = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидДоговора")[0];
		
		Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
		
			Элементы.КнопкаЗаполнить.Доступность = Истина;
			Возврат Истина;
			
		КонецЕсли;
				
	КонецЕсли;
	
	Возврат Ложь;
		
КонецФункции

&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	
	Если ЗначениеЗаполнено(Объект.Услуги) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("АвтозаполнитьСтрокиТабличнойЧастиУслуги", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения, "Введенные ранее данные будут утеряны." + Символы.ПС + "Вы уверены?", РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
	Иначе
		АвтозаполнитьСтрокиТабличнойЧастиУслуги(КодВозвратаДиалога.Да, Новый Структура);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтозаполнитьСтрокиТабличнойЧастиУслуги(РезультатВыбора, ДопПараметры) Экспорт

	Если РезультатВыбора = КодВозвратаДиалога.Да Тогда
		
		АвтозаполнитьСтрокиТабличнойЧастиУслугиНаСервере(); 
		ЭтотОбъект.ОбновитьОтображениеДанных();

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура АвтозаполнитьСтрокиТабличнойЧастиУслугиНаСервере() 
	
	Если ЭтотОбъект.Объект.Ссылка.Пустая() Тогда
		ОбщегоНазначения.СообщитьПользователю("Автозаполнение невозможно. Необходимо записать документ!");
	Иначе	
		ДокументОбъект = ЭтотОбъект.Объект.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ВКМ_ВыполнитьАвтозаполнение();
		ЭтотОбъект.Прочитать();
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
		Элементы.КнопкаЗаполнить.Доступность = ДоговорПриИзмененииНаСервере();
		
КонецПроцедуры

&НаСервере
Функция ДоговорПриИзмененииНаСервере()
	
	Возврат ВКМНастроитьДоступностьКомандыЗаполненияДокумента();
	
КонецФункции

#КонецОбласти

	//--