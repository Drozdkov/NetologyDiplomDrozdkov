
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Начисления.Количество() <> 0 Тогда
	
		РезультатЗапроса = ПолучитьДополнительныеДанныеПоСотрудникам();
		
		//++ Исправление замечаний к дипломной работе 30.07.24
        //Получение списка сотрудников для формирования движения удержания НДФЛ
		
		МассивСотрудниковСДублями = РезультатЗапроса.ВыгрузитьКолонку("Сотрудник");
		СписокСотрудников = Новый Соответствие;
		
		Для Каждого Сотрудник Из МассивСотрудниковСДублями  Цикл
			Повтор = СписокСотрудников.Получить(Сотрудник);
			Если Повтор = Неопределено Тогда
				СписокСотрудников.Вставить(Сотрудник,1);
			КонецЕсли;
		КонецЦикла;  
		
		МассивСотрудниковБезДублей = Новый Массив;
		
		Для каждого Сотрудник Из СписокСотрудников Цикл
			МассивСотрудниковБезДублей.Добавить(Сотрудник.Ключ);
		КонецЦикла;
		
		//--
		
		ЕстьСотрудникиСНеустановленнымиОкладамиИлиПремиями = Ложь;
		МассивСотрудниковСНеустановленнымиОкладамиИлиПремиями = Новый Массив;
		
		Для каждого Строка из РезультатЗапроса Цикл
		
			Если НЕ ЗначениеЗаполнено(Строка.Оклад) Тогда	
				ЕстьСотрудникиСНеустановленнымиОкладамиИлиПремиями = Истина;
				МассивСотрудниковСНеустановленнымиОкладамиИлиПремиями.Добавить(Строка(Строка.Сотрудник));
			Иначе
				Если Строка.Категория = "Специалист" ИЛИ НЕ ЗначениеЗаполнено(Строка.Категория) Тогда
					Если НЕ ЗначениеЗаполнено(Строка.ПроцентОтРабот)Тогда
						ЕстьСотрудникиСНеустановленнымиОкладамиИлиПремиями = Истина;
						МассивСотрудниковСНеустановленнымиОкладамиИлиПремиями.Добавить(Строка(Строка.Сотрудник));
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ ЕстьСотрудникиСНеустановленнымиОкладамиИлиПремиями Тогда
		
			НоваяТЗдляВставкиВТЧ = РезультатЗапроса.Скопировать(,"Сотрудник, ВидРасчета, ДатаНачала, ДатаОкончания");
			
			СформироватьДвижения(РезультатЗапроса);
			РассчитатьОкладПремиюОтпуск();

			Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
			//Движения.ВКМ_Удержания.Записать();
			Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
			Движения.ВКМ_ОтпускаСотрудников.Записать();
			
			//++ Исправление замечаний к дипломной работе 30.07.24
			СформироватьДвижениеУдержания(МассивСотрудниковБезДублей);
			Движения.ВКМ_Удержания.Записать();
            //--
			
			ДокументОбъект = Ссылка.ПолучитьОбъект();
			ДокументОбъект.Начисления.Очистить();
			ДокументОбъект.Начисления.Загрузить(НоваяТЗдляВставкиВТЧ);	
			ДокументОбъект.Записать();	
		
		Иначе
			
			СписокСотрудниковСНеустановленнымиОкладамиИлиПремиями = СтрСоединить(МассивСотрудниковСНеустановленнымиОкладамиИлиПремиями, ", ");
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = "Для следующих сотрудников не установлены оклад или премия в указанном периоде:" + Символы.ПС + СписокСотрудниковСНеустановленнымиОкладамиИлиПремиями;
			Сообщение.Сообщить();	
			
			Отказ = Истина;
			
		КонецЕсли;		
				
	Иначе
			
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Табличная часть документа не может быть пустой";
		Сообщение.Сообщить();			
		
		Отказ = Истина;
		
	КонецЕсли;
		
КонецПроцедуры

Функция ПолучитьДополнительныеДанныеПоСотрудникам()

		// обогащение данных из табличной части документа данными об окладе, категории сотрудника и проценте премии
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	НАЧАЛОПЕРИОДА(ВКМ_НачисленияЗарплатыНачисления.ДатаНачала, МЕСЯЦ) КАК Период,
		               |	ВКМ_НачисленияЗарплатыНачисления.Сотрудник КАК Сотрудник,
		               |	ВКМ_НачисленияЗарплатыНачисления.НомерСтроки КАК НомерСтроки,
		               |	ВКМ_НачисленияЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
		               |	ВКМ_НачисленияЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания,
		               |	ВКМ_НачисленияЗарплатыНачисления.Ссылка КАК Ссылка,
		               |	ВКМ_НачисленияЗарплатыНачисления.ВидРасчета КАК ВидРасчета
		               |ПОМЕСТИТЬ ДанныеДокумента
		               |ИЗ
		               |	Документ.ВКМ_НачисленияЗарплаты.Начисления КАК ВКМ_НачисленияЗарплатыНачисления
		               |ГДЕ
		               |	ВКМ_НачисленияЗарплатыНачисления.Ссылка = &Ссылка
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
		               |	МАКСИМУМ(ВКМ_УсловияОплатыСотрудников.Период) КАК Период
		               |ПОМЕСТИТЬ ПериодОклада
		               |ИЗ
		               |	ДанныеДокумента КАК ДанныеДокумента
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
		               |		ПО ДанныеДокумента.Сотрудник = ВКМ_УсловияОплатыСотрудников.Сотрудник
		               |			И ДанныеДокумента.Период >= ВКМ_УсловияОплатыСотрудников.Период
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ДанныеДокумента.НомерСтроки
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ДанныеДокумента.Сотрудник КАК Сотрудник,
		               |	ДанныеДокумента.ВидРасчета КАК ВидРасчета,
		               |	ДанныеДокумента.ДатаНачала КАК ДатаНачала,
		               |	ДанныеДокумента.ДатаОкончания КАК ДатаОкончания,
		               |	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
		               |	ДанныеДокумента.Период КАК Период,
		               |	ВКМ_УсловияОплатыСотрудников.Оклад КАК Оклад,
		               |	ВКМ_УсловияОплатыСотрудников.ПроцентОтРабот КАК ПроцентОтРабот,
		               |	ВКМ_УсловияОплатыСотрудников.Категория КАК Категория
		               |ИЗ
		               |	ДанныеДокумента КАК ДанныеДокумента
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ПериодОклада КАК ПериодОклада
		               |		ПО ДанныеДокумента.НомерСтроки = ПериодОклада.НомерСтроки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников КАК ВКМ_УсловияОплатыСотрудников
		               |		ПО ДанныеДокумента.Сотрудник = ВКМ_УсловияОплатыСотрудников.Сотрудник
		               |			И ДанныеДокумента.Период >= ВКМ_УсловияОплатыСотрудников.Период
		               |			И (ПериодОклада.Период = ВКМ_УсловияОплатыСотрудников.Период)";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);	
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();	
		
		// преобразование таблицы (обеспечение наличия премии процентом для всех сотрудников категории "Специалист"
		
		СписокНачислений = Новый ТаблицаЗначений;
		СписокНачислений.Колонки.Добавить("Сотрудник");
		СписокНачислений.Колонки.Добавить("Категория");		
		СписокНачислений.Колонки.Добавить("ВидРасчета");		
		СписокНачислений.Колонки.Добавить("ДатаНачала");			
		СписокНачислений.Колонки.Добавить("ДатаОкончания");			
		СписокНачислений.Колонки.Добавить("Оклад");	
		СписокНачислений.Колонки.Добавить("ПроцентОтРабот");			

		Для Каждого Строка из РезультатЗапроса Цикл
			
			Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
								
				НоваяЗапись = СписокНачислений.Добавить();
				НоваяЗапись.Сотрудник = Строка.Сотрудник;
				НоваяЗапись.Категория = Строка.Категория;
				НоваяЗапись.ВидРасчета = Строка.ВидРасчета;
				НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
				НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
				НоваяЗапись.Оклад = Строка.Оклад;
				НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
					
				Если Строка.Категория = Перечисления.ВКМ_КатегорииСотрудников.Специалист Тогда
					
					НоваяЗапись = СписокНачислений.Добавить();
					НоваяЗапись.Сотрудник = Строка.Сотрудник;
					НоваяЗапись.Категория = Строка.Категория;
					НоваяЗапись.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом;
					НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
					НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
					НоваяЗапись.Оклад = Строка.Оклад;
					НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
			
				КонецЕсли;
			
			ИначеЕсли Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
				
				НоваяЗапись = СписокНачислений.Добавить();
				НоваяЗапись.Сотрудник = Строка.Сотрудник;
				НоваяЗапись.Категория = Строка.Категория;
				НоваяЗапись.ВидРасчета = Строка.ВидРасчета;
				НоваяЗапись.ДатаНачала = Строка.ДатаНачала;
				НоваяЗапись.ДатаОкончания = Строка.ДатаОкончания;
				НоваяЗапись.Оклад = Строка.Оклад;
				НоваяЗапись.ПроцентОтРабот = Строка.ПроцентОтРабот;	
				
			Иначе Продолжить;	
				
			КонецЕсли;
			
		КонецЦикла;
					
		Возврат СписокНачислений;
	
КонецФункции

Процедура СформироватьДвижения(РезультатЗапроса)
	
	Для каждого Строка Из   РезультатЗапроса Цикл
	
		Если ТипЗнч(Строка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ОсновныеНачисления") Тогда

			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.Сторно = Ложь;
			Движение.ВидРасчета = Строка.ВидРасчета;
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания;
			Движение.ПериодРегистрации = Дата;		
			Движение.Сотрудник = Строка.Сотрудник;
			
			Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
				
				Движение.Показатель = Строка.Оклад;  
				//++Исправление замечаний к дипломной работе 30.07.2024
				//Движение.БазовыйПериодНачало = НачалоМесяца(Строка.ДатаНачала);
				//Движение.БазовыйПериодКонец = КонецМесяца(Строка.ДатаОкончания);
				//--

				
			ИначеЕсли Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
				
				Движение.БазовыйПериодНачало = ДобавитьМесяц(Строка.ДатаНачала, -12);
				Движение.БазовыйПериодКонец = Строка.ДатаОкончания - 86400;	
				Движение.КоличествоДней = (Строка.ДатаОкончания - Строка.ДатаНачала)/86400 + 1;
				Движение.Показатель = 0;  
				
			ИначеЕсли Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом Тогда
				
				Движение.Показатель = Строка.ПроцентОтРабот;
				//++Исправление замечаний к дипломной работе 30.07.2024
				//Движение.БазовыйПериодНачало = НачалоМесяца(Строка.ДатаНачала);
				//Движение.БазовыйПериодКонец = КонецМесяца(Строка.ДатаОкончания);
				//--

            КонецЕсли;
						
		КонецЕсли;
		
	КонецЦикла;
	
	СформироватьСторноЗаписи();
     
	Движения.ВКМ_ОсновныеНачисления.Записать();
 
КонецПроцедуры

Процедура СформироватьСторноЗаписи()
	
	НаборДвиженийРегистра = Движения.ВКМ_ОсновныеНачисления;
	
	
	СторноЗаписи = Движения.ВКМ_ОсновныеНачисления.ПолучитьДополнение();
	
	Для каждого Строка из СторноЗаписи Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Строка);
		
		Движение.ПериодРегистрации = Дата;
		Движение.ПериодДействияНачало = Строка.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Строка.ПериодДействияКонецСторно;  
		Движение.Сторно = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьОкладПремиюОтпуск()

	Запрос = Новый Запрос;
	
	Запрос.Текст =   "ВЫБРАТЬ
	                 |	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейПериодДействия, 0) КАК План,
	                 |	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейФактическийПериодДействия, 0) КАК Факт,
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодРегистрации КАК ПериодРегистрации,
	                 |	СУММА(ВКМ_ВыполненныеСотрудникомРаботы.СуммаКОплате) КАК СуммаКОплате
	                 |ИЗ
	                 |	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	                 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы КАК ВКМ_ВыполненныеСотрудникомРаботы
	                 |		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботы.Сотрудник
	                 |			И (ВКМ_ВыполненныеСотрудникомРаботы.Период МЕЖДУ НАЧАЛОПЕРИОДА(ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало, МЕСЯЦ) И КОНЕЦПЕРИОДА(ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец, МЕСЯЦ))
	                 |ГДЕ
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.Регистратор = &Регистратор
	                 |
	                 |СГРУППИРОВАТЬ ПО
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета,
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.ПериодРегистрации,
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник,
	                 |	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки,
	                 |	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейФактическийПериодДействия, 0),
	                 |	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейПериодДействия, 0)" ;

	Запрос.УстановитьПараметр("Регистратор", Ссылка); 
		
	Выборка = Запрос.Выполнить().Выбрать(); 
	
	ЕстьОтпуск = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
//		Блокировка = Новый БлокировкаДанных;
//		
//		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками");
//		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
//		ЭлементБлокировки.УстановитьЗначение("Сотрудник", Выборка.Сотрудник);
//		Блокировка.Заблокировать(); 
//		
//		Движения.ВКМ_ВзаиморасчетыССотрудниками.Очистить();
//		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
//		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать(); 
		
		Если Выборка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад Тогда
		
			ДвижениеРасчет = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];  
			ДвижениеРасчет.Сумма = ДвижениеРасчет.Показатель * Выборка.Факт / Выборка.План;
			ДвижениеРасчет.КоличествоДней = Выборка.Факт;

			Если ДвижениеРасчет.Сторно Тогда
				
				ДвижениеРасчет.Сумма = - ДвижениеРасчет.Сумма;
				ДвижениеРасчет.КоличествоДней = - ДвижениеРасчет.КоличествоДней;
				
			КонецЕсли; 
			
			Если ДвижениеРасчет.Сумма <> 0 Тогда
			
				//СформироватьДвижениеУдержания(ДвижениеРасчет);
				СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(ДвижениеРасчет);
				
			КонецЕсли;	
			
		ИначеЕсли Выборка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцентом Тогда
			
			ДвижениеРасчет = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];  
			ДвижениеРасчет.Сумма = Выборка.СуммаКОплате;

			Если ДвижениеРасчет.Сторно Тогда
				
				ДвижениеРасчет.Сумма = - ДвижениеРасчет.Сумма;
				
			КонецЕсли; 
			
			Если ДвижениеРасчет.Сумма <> 0 Тогда
			
				//СформироватьДвижениеУдержания(ДвижениеРасчет);
				СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(ДвижениеРасчет);
				
			КонецЕсли;	
			
		ИначеЕсли Выборка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда
        			
			ЕстьОтпуск = Истина;
		
	КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);		
	
	Если ЕстьОтпуск = Истина Тогда
		
		РассчитатьОтпуск();	
		
	КонецЕсли; 
		
КонецПроцедуры

Процедура РассчитатьОтпуск()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 	"ВЫБРАТЬ
	               	|	ВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	               	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.СуммаБаза, 0) КАК ОснНачисления,
	               	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.СуммаБаза, 0) КАК ДопНачисления,
	               	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейФактическийПериодДействия, 0) КАК План,
	               	|	ВКМ_ОсновныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	               	|	ВКМ_ОсновныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	               	|	ВКМ_ОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации,
	               	|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
	               	|	ВКМ_ОсновныеНачисления.ВидРасчета КАК ВидРасчета,
	               	|	ЕСТЬNULL(ВКМ_ОсновныеНачисления.КоличествоДней, 0) КАК Факт1,
	               	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.КоличествоДнейБаза, 0) КАК Факт
	               	|ИЗ
	               	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	               	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
	               	|				&Измерения,
	               	|				&Измерения,
	               	|				,
	               	|				Регистратор = &Ссылка
	               	|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
	               	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
	               	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(
	               	|				&Измерения,
	               	|				&Измерения,
	               	|				,
	               	|				Регистратор = &Ссылка
	               	|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
	               	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки
	               	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	               	|				Регистратор = &Ссылка
	               	|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	               	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки
	               	|ГДЕ
	               	|	ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск
	               	|	И ВКМ_ОсновныеНачисления.Регистратор = &Ссылка";
	
					
	Измерения = Новый Массив();
	Измерения.Добавить("Сотрудник");
				
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка <> Неопределено Тогда     
		
		Пока Выборка.Следующий() Цикл 
			
			БлокировкаОтпуска = Новый БлокировкаДанных;
					
			ЭлементБлокировки = БлокировкаОтпуска.Добавить("РегистрНакопления.ВКМ_ОтпускаСотрудников");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Сотрудник", Выборка.Сотрудник);
			БлокировкаОтпуска.Заблокировать(); 
			
			Движения.ВКМ_ОтпускаСотрудников.Очистить();
			Движения.ВКМ_ОтпускаСотрудников.Записывать = Истина;
			Движения.ВКМ_ОтпускаСотрудников.Записать(); 
			
			Если Выборка.Факт <> 0 Тогда
				
				ДвижениеРасчет = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1]; 
				
				КоличествоДнейОтпуска = Выборка.Факт1;
				СредняяДневнаяСтавка = (Выборка.ОснНачисления + Выборка.ДопНачисления)/Выборка.Факт; 
				
				ДвижениеРасчет.КоличествоДней = КоличествоДнейОтпуска;
				ДвижениеРасчет.Сумма = КоличествоДнейОтпуска * СредняяДневнаяСтавка; 
				ДвижениеРасчет.Показатель = СредняяДневнаяСтавка;
				
				//СформироватьДвижениеУдержания(ДвижениеРасчет);
				СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(ДвижениеРасчет);
				СформироватьРасходПоРегиструНакопленияОтпуск(ДвижениеРасчет, КоличествоДнейОтпуска);

			Иначе 
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("В базовом периоде у сотрудника %1 не было рабочих дней, отпускные не начислены.", Выборка.Сотрудник);  
				Сообщение.Сообщить ();
				
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвижениеУдержания(МассивСотрудниковБезДублей)
	
	Для каждого Сотрудник из  МассивСотрудниковБезДублей Цикл
		
		ДвижениеУдержание = Движения.ВКМ_Удержания.Добавить();
		ДвижениеУдержание.Сторно = Ложь;
		ДвижениеУдержание.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
		ДвижениеУдержание.БазовыйПериодНачало = НачалоМесяца(Дата);
		ДвижениеУдержание.БазовыйПериодКонец = НачалоДня(КонецМесяца(Дата));	
		ДвижениеУдержание.ПериодДействияНачало = НачалоМесяца(Дата);
		ДвижениеУдержание.ПериодДействияКонец = НачалоДня(КонецМесяца(Дата));
		ДвижениеУдержание.ПериодРегистрации = Дата;
		ДвижениеУдержание.Сотрудник = Сотрудник;

	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать();
    РасчитатьНДФЛ();
	
КонецПроцедуры

Процедура РасчитатьНДФЛ()

	Запрос = Новый Запрос;

// Текст запроса до изменения 05.08.24	
	
//	Запрос.Текст = 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
//	|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
//	|	МАКСИМУМ(ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.СуммаБаза, 0)) КАК ОснНачисления,
//	|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
//	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.БазовыйПериодНачало,
//	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.БазовыйПериодКонец
//	|ПОМЕСТИТЬ СуммаБаза
//	|ИЗ
//	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
//	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,, ВидРасчета = &НДФЛ
//	|		И Регистратор = &Ссылка) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления
//	|		ПО ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки
//	|		И ВКМ_Удержания.Сотрудник = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Сотрудник
//	|ГДЕ
//	|	ВКМ_Удержания.Регистратор = &Ссылка
//	|	И ВКМ_Удержания.ВидРасчета = &НДФЛ
//	|СГРУППИРОВАТЬ ПО
//	|	ВКМ_Удержания.Сотрудник,
//	|	ВКМ_Удержания.НомерСтроки,
//	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.БазовыйПериодНачало,
//	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.БазовыйПериодКонец
//	|;
//	|
//	|////////////////////////////////////////////////////////////////////////////////
//	|ВЫБРАТЬ
//	|	ВКМ_Удержания.Сотрудник,
//	|	СУММА(ЕСТЬNULL(ВКМ_Удержания.Сумма, 0)) КАК СуммаУдержанногоНДФЛ,
//	|	ВКМ_Удержания.БазовыйПериодНачало,
//	|	ВКМ_Удержания.БазовыйПериодКонец
//	|ПОМЕСТИТЬ УдержанныйНДФЛ
//	|ИЗ
//	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
//	|ГДЕ
//	|	ВКМ_Удержания.Комментарий = &Комментарий
//	|СГРУППИРОВАТЬ ПО
//	|	ВКМ_Удержания.Сотрудник,
//	|	ВКМ_Удержания.БазовыйПериодНачало,
//	|	ВКМ_Удержания.БазовыйПериодКонец
//	|;
//	|
//	|////////////////////////////////////////////////////////////////////////////////
//	|ВЫБРАТЬ
//	|	СуммаБаза.Сотрудник,
//	|	СуммаБаза.ОснНачисления,
//	|	ЕСТЬNULL(УдержанныйНДФЛ.СуммаУдержанногоНДФЛ, 0) КАК СуммаУдержанногоНДФЛ,
//	|	СуммаБаза.НомерСтроки
//	|ИЗ
//	|	СуммаБаза КАК СуммаБаза
//	|		ЛЕВОЕ СОЕДИНЕНИЕ УдержанныйНДФЛ КАК УдержанныйНДФЛ
//	|		ПО УдержанныйНДФЛ.Сотрудник = СуммаБаза.Сотрудник
//	|		И СуммаБаза.БазовыйПериодНачало = УдержанныйНДФЛ.БазовыйПериодНачало
//	|		И СуммаБаза.БазовыйПериодКонец = УдержанныйНДФЛ.БазовыйПериодКонец"; 

	Запрос.Текст = 	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	               	|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
	               	|	ВКМ_Удержания.НомерСтроки КАК НомерСтроки,
	               	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.БазовыйПериодНачало КАК БазовыйПериодНачало,
	               	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.БазовыйПериодКонец КАК БазовыйПериодКонец,
	               	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РегистраторРазрез КАК РегистраторРазрез,
	               	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.СуммаБаза КАК ОснНачисления
	               	|ИЗ
	               	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	               	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ОсновныеНачисления(
	               	|				&Измерения,
	               	|				&Измерения,
	               	|				&Разрезы,
	               	|				ВидРасчета = &НДФЛ
	               	|					И Регистратор = &Ссылка) КАК ВКМ_УдержанияБазаВКМ_ОсновныеНачисления
	               	|		ПО ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.НомерСтроки
	               	|			И ВКМ_Удержания.Сотрудник = ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.Сотрудник
	               	|ГДЕ
	               	|	ВКМ_Удержания.Регистратор = &Ссылка
	               	|	И ВКМ_Удержания.ВидРасчета = &НДФЛ
	               	|	И ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РегистраторРазрез = &Ссылка
	               	|
	               	|СГРУППИРОВАТЬ ПО
	               	|	ВКМ_Удержания.Сотрудник,
	               	|	ВКМ_Удержания.НомерСтроки,
	               	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.БазовыйПериодНачало,
	               	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.БазовыйПериодКонец,
	               	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.РегистраторРазрез,
	               	|	ВКМ_УдержанияБазаВКМ_ОсновныеНачисления.СуммаБаза"; 
	
	Измерения = Новый Массив();
	Измерения.Добавить("Сотрудник");

	//++ Исправление замечаний к дипломной работе 05.08.24
	Разрезы = Новый Массив();
	Разрезы.Добавить("Регистратор");  
	
	Запрос.УстановитьПараметр("Разрезы", Разрезы);
	//--
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
    Запрос.УстановитьПараметр("Ссылка", Ссылка);  
	Запрос.УстановитьПараметр("НДФЛ", ПланыВидовРасчета.ВКМ_Удержания.НДФЛ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка <> Неопределено Тогда     
		
		Пока Выборка.Следующий() Цикл 
			
			Если Выборка.ОснНачисления <> 0 Тогда
				
				ДвижениеРасчетНДФЛ = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1]; 

				
				// Исправление замечаний к дипломной работе 05.08.24 - изменил формулу начисления НДФЛ (ранее была проверка на уже начисленный НДФЛ в указанном периоде)

				СтавкаНДФЛ = 0.13;
				СуммаНДФЛНачислено = Выборка.ОснНачисления*СтавкаНДФЛ;
								
				ДвижениеРасчетНДФЛ.Сумма = СуммаНДФЛНачислено; 
				ДвижениеРасчетНДФЛ.Комментарий = ПолучитьКомментарийКДвижениямПоНДФЛДляОсновныхНачислений();
				
				СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(ДвижениеРасчетНДФЛ);
				
			Иначе 
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Сумма НДФЛ не рассчитана. Обратитесь к администратору системы";  
				Сообщение.Сообщить ();
				
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьКомментарийКДвижениямПоНДФЛДляОсновныхНачислений()
	
	Комментарий = "Оклад, Отпуск, Премия процентом";
	
	Возврат Комментарий;
	
КонецФункции

Процедура СформироватьПриходПоРегиструВзаиморасчетыССотрудниками(Движение)
	
	Если Движение.Сумма <> 0 Тогда  
		
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
		ДвижениеНакопление = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		ДвижениеНакопление.Период = Движение.ПериодДействияКонец;
		ДвижениеНакопление.Сотрудник = Движение.Сотрудник;
		ДвижениеНакопление.ВидРасчета = Движение.ВидРасчета;
		ДвижениеНакопление.Сумма = Движение.Сумма; 
			
	КонецЕсли;		
	
КонецПроцедуры

Процедура СформироватьРасходПоРегиструВзаиморасчетыССотрудниками(Движение)
	
	Если Движение.Сумма <> 0 Тогда
		
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
		ДвижениеНакопление = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьРасход();
		ДвижениеНакопление.Период = Движение.ПериодДействияКонец;
		ДвижениеНакопление.Сотрудник = Движение.Сотрудник;
		ДвижениеНакопление.ВидРасчета = Движение.ВидРасчета;
		ДвижениеНакопление.Сумма = Движение.Сумма; 
		ДвижениеНакопление.Комментарий = Движение.Комментарий;
			
	КонецЕсли;		
	
КонецПроцедуры

Процедура СформироватьРасходПоРегиструНакопленияОтпуск(Движение, КоличествоДнейОтпуска)
	
	Если КоличествоДнейОтпуска <> 0 Тогда
		
		Движения.ВКМ_ОтпускаСотрудников.Записывать = Истина;
		ДвижениеНакопление = Движения.ВКМ_ОтпускаСотрудников.ДобавитьРасход();
		ДвижениеНакопление.Период = Движение.ПериодДействияКонец;
		ДвижениеНакопление.Сотрудник = Движение.Сотрудник;
		ДвижениеНакопление.КоличествоДней = КоличествоДнейОтпуска; 
			
	КонецЕсли;		
	
КонецПроцедуры      
